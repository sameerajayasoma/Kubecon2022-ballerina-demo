import ballerina/http;
import ballerina/io;

configurable string taskGroupServiceUrl = ?;
configurable string taskServiceUrl = ?;

type Group record {
    int id;
    string title;
    string description;
    string createdAt;
    string updatedAt;
};

type Task record {
    int id;
    string title;
    string description;
    int groupId;
    string status;
    string createdAt;
    string updatedAt;
};

public function main() returns error? {
    string title = "foo";
    http:Client taskGroupEp = check new (taskGroupServiceUrl);
    Group[] groups = check taskGroupEp->get("/groups");

    Group[] filteredGroups = from var group in groups
        where group.title == title
        select group;

    if filteredGroups.length() == 0 {
        return error(string `Cannot find a group with title: ${title}`);
    }

    http:Client taskEp = check new (taskServiceUrl);
    Task[] tasks = check taskEp->/tasks(groupId = filteredGroups[0].id);
    io:println(tasks);
}
